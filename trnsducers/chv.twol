Alphabet				
  а ӑ е ё ӗ и о у ӳ ы э ю я б в г д ж з к л м н п р с ҫ т ф х ц ч ш щ й ь ъ				
  А Ӑ Е Ё Ӗ И О У Ӳ Ы Э Ю Я Б В Г Д Ж З К Л М Н П Р С Ҫ Т Ф Х Ц Ч Ш Щ Й Ь Ъ				
 %{A%}:а %{A%}:е				
 %{Ă%}:ӑ %{Ă%}:ӗ %{Ă%}:0				
 %{м%}:м %{м%}:0 %{м%}:н				
 %{н%}:н %{н%}:0				
 %{р%}:р %{р%}:т %{р%}:ч				
 %{ъ%}:0				
 %{ь%}:0				
 %{я%}:0 %{я%}:йӑ %{я%}:йӗ				
 %{э%}:ӑ %{э%}:ӗ %{э%}:0				
 %{У%}:у %{У%}:ю %{У%}:ӳ
 %{С%}:0				
;				
				
				
Sets				
				
BackVow = ӑ а ы о у я ё ю ;				
				
FrontVow = е ӗ э и ӳ ;				
				
Vow = ӑ а ы о у я ё ю е ӗ э и ӳ ;				
				
ArchiBack = %{ъ%} ;				
				
Cns = б в г д ж з к л м н п р с ҫ т ф х ц ч ш щ й ъ ;				
				
ArchiCns = %{м%} %{н%} %{р%}  %{С%} ;				
				

Rules				
				
"Remove morpheme boundary"				
%>:0 <=> _ ;				
				
"Back vowel harmony for archiphoneme {A}"				
%{A%}:а <=> [ BackVow: | ArchiBack: ] [ ArchiCns: | Cns: | %{э%}: | %{я%}: | %{ь%}: | %{Ă%}: | %{У%}: | %>: ]+ _ ;				
				
				
"Back vowel harmony for archiphoneme {Ă}"				
%{Ă%}:ӑ <=> [ BackVow: | ArchiBack: ] [ ArchiCns: | Cns: | %{э%}: | %{я%}: | %{ь%}: | %{Ă%}: | %{У%}: | %>: ]+ _ ;				
        except				
				[ %{м%}: | BackVow: | FrontVow: | %{э%}: | %{я%}: | %{ь%}: | %{У%}: ] %>: _ [ н | м ] ;
				
				
"No surface {Ă} in plural genetive  or after a vowel in stem"				
%{Ă%}:0 <=> [ BackVow: | FrontVow: | %{м%}: | %{я%}: | %{э%}: | и й: | %{У%}: ] [ %{ь%}: ]* %>: _ [ н | м ];
	except
		и: [ й: ]* %>: _ м ; !Possessive for шаши, информаци	

				
				
"No surface {м} in plural genetive and dative"				
%{м%}:0 <=> _ %>: [ %{н%}: | %{Ă%}: н ] ;				
				
"Surface {м} as н in plural locative and ablative"
%{м%}:н <=> _ %>: %{р%}: %{A%}: ;

"Surface {р} as т after р in locative and ablative"				
 %{р%}:т <=> р %>: _ %{A%}: ;				
				
"Remove duplicate consonant in cases"				
Cons:0 <=> Cons _ %>: ; where Cons in Cns ;				
				
!"йӑ"				
!я:йӑ <=> _  %{я%}: %>: ;				
				
"ьӑ"				
я:ьӑ <=> _  %{ь%}: %>: ;				
				
"Remove ь before vowel in affix or remove duplicate ь"				
ь:0 <=> _ я: %{ь%}: %>: ; ! Наталья				
	_ %>: [ %{Ă%}: | %{A%}: | %{У%}: ] ;			
	_ е: %{я%}: %>: ; ! платье			
				
"Remove vowel before {э} and  {я} in cases"				
V:0 <=> _ [ %{э%}: | %{я%}: ] %>: ; 
	except
		_  %{я%}:0 %>: ;
	where V in Vow ;				
			
				
"No surface {я} in nominative and px1sg after е"				
%{я%}:0 <=> _ #;
		_ %>: %{У%}: ;
!	   е: _ %>: %{Ă%}: м ;  !платье
				
"Back vowel harmony for archiphoneme {я}"				
%{я%}:йӑ <=> BackVow: _ %>: ;
	except 
		 _ %>: %{У%}: ;
				
				
"No surface {э} in nominative"				
%{э%}:0 <=> _ # ;				
		_ %>: %{У%}: ;
				
"Back vowel harmony for archiphoneme {э}"				
%{э%}:ӑ <=> BackVow: _ %>: ;
	except 
		 _ %>: %{У%}: ;				
				
"Duplicate consonants in 1-2 syllables in genetive"				
%{С%}:C <=> C _ [ Vow: ]* %>: [ %{Ă%}: | %{A%}: | %{У%}: ] ; where C in Cns ;				
				
"Remove surface {н} in affix after a consonant"				
%{н%}:0 <=> C: %>: _ ; where C in Cns ;				
				
"Remove surface {й} at the end of the stem after и"				
!Золотницкий			
й:0 <=> и _ %>: ;
	except 
		и _ %>: %{Ă%}: м ; !px1sg

"Add й after и in Possessive 1sg"
и:ий <=> _ %>: %{Ă%}: м ;
				
"Surface {ч} in dative plural"				
%{р%}:ч <=> %{м%}: %>: _ ;				

"No surface ҫ in nominative"
!власть
ҫ:сть <=> _ # ;


"Remove surface vowel before {У%}" 
V:0 <=> _ %>: %{У%}: ; where V in Vow ; 


"Back vowel а, ӑ, о  harmony for archiphoneme {У}" 
%{У%}:у <=> [ а: | ӑ: | о: | ArchiBack: ] [ Cns: | ArchiCns: | %{э%}: | %>: ]+ _ ;
	except 
		[ а: | ӑ: | о: ] й: %>: _ ;

"Back vowel я and й  harmony for archiphoneme {У}"
%{У%}:ю <=>  я: [ %{я%}: | %{ь%}: ]* %>: _ ;
	    [ а: | ӑ: | о: ] й: %>: _ ;

!"Surface ю in px2sg"
!%{У%}:ю <=> Vow: й: %>: _ ;
